# load zgenom
source "${HOME}/.zgenom/zgenom.zsh"

zgenom autoupdate

source ~/.zsh_aliases

# .zsh_extra is a file that is not synced with chezmoi, if it exists source it
if [[ -f ~/.zsh_extra ]]; then
    source ~/.zsh_extra
fi

# if the init script doesn't exist
if ! zgenom saved; then

    # Enable autocompletion based on history
    zgenom load zsh-users/zsh-autosuggestions

    # Extra completions
    zgenom load zsh-users/zsh-completions

    # Enable shell syntax highlighting
    zgenom load zsh-users/zsh-syntax-highlighting

    zgenom load Aloxaf/fzf-tab

    # Load .envrc files from current directory
    zgenom ohmyzsh plugins/direnv

    # save all to init script
    zgenom save

    # Compile your zsh files
    zgenom compile "$HOME/.zshrc"
fi

# Check for plugin and zgenom updates every 7 days
# This does not increase the startup time.
zgenom autoupdate

# Set some history options
setopt append_history
setopt extended_history
setopt hist_expire_dups_first
setopt hist_ignore_all_dups
setopt hist_ignore_dups
setopt hist_ignore_space
setopt hist_reduce_blanks
setopt hist_save_no_dups
setopt hist_verify
setopt INC_APPEND_HISTORY
unsetopt HIST_BEEP

# Share your history across all your terminal windows
setopt share_history

# Keep a ton of history
export HISTSIZE=100000
export SAVEHIST=100000
HISTFILE=~/.zsh_history

# Use emacs-style keys by default (or use -v for vi mode)
bindkey -e

# Load terminfo so we can bind using portable names
zmodload zsh/terminfo

# Helper to safely bind if the terminfo key exists
bind_if_present() {
  local key="$1" widget="$2"
  [[ -n "$key" ]] && bindkey "$key" "$widget"
}

# Core keys
bind_if_present "${terminfo[kdch1]}" delete-char            # Delete
bind_if_present "${terminfo[khome]}" beginning-of-line      # Home
bind_if_present "${terminfo[kend]}"  end-of-line            # End
bind_if_present "${terminfo[kich1]}" overwrite-mode         # Insert

# Arrows (usually already work, but ensure anyway)
bind_if_present "${terminfo[kcuu1]}" up-line-or-history
bind_if_present "${terminfo[kcud1]}" down-line-or-history
bind_if_present "${terminfo[kcub1]}" backward-char
bind_if_present "${terminfo[kcuf1]}" forward-char

# Common fallbacks (some terminals send these)
bindkey '\e[H'  beginning-of-line
bindkey '\e[F'  end-of-line
bindkey '\e[3~' delete-char

# Add some completions settings
setopt ALWAYS_TO_END    # Move cursor to the end of a completed word.
setopt AUTO_LIST        # Automatically list choices on ambiguous completion.
setopt AUTO_MENU        # Show completion menu on a successive tab press.
setopt AUTO_PARAM_SLASH # If completed parameter is a directory, add a trailing slash.
setopt COMPLETE_IN_WORD # Complete from both ends of a word.
unsetopt MENU_COMPLETE  # Do not autoselect the first completion entry.

# Miscellaneous settings
setopt INTERACTIVE_COMMENTS # Enable comments in interactive shell.
setopt extended_glob        # Enable more powerful glob features

# Speed up autocomplete, force prefix mapping
zstyle ':completion:*' accept-exact '*(N)'
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.zsh/cache

# disable sort when completing `git checkout`
zstyle ':completion:*:git-checkout:*' sort false
# set descriptions format to enable group support
# NOTE: don't use escape sequences here, fzf-tab will ignore them
zstyle ':completion:*:descriptions' format '[%d]'
# set list-colors to enable filename colorizing
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
# force zsh not to show completion menu, which allows fzf-tab to capture the unambiguous prefix
zstyle ':completion:*' menu no
# preview directory's content with eza when completing cd
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'eza -1 --color=always $realpath'
# switch group using `<` and `>`
zstyle ':fzf-tab:*' switch-group '<' '>'

VISUAL='code --wait --new-window'
EDITOR='vim'

export VISUAL EDITOR

# Add completion for SSH known hosts
zstyle -e ':completion:*:(ssh|scp|sftp|rsh|rsync):hosts' hosts 'reply=(${=${${(f)"$(cat {/etc/ssh_,~/.ssh/known_}hosts(|2)(N) /dev/null)"}%%[# ]*}//,/ })'

# Add yazi shell wrapper allowing cd functionality
function y() {
    local tmp="$(mktemp -t "yazi-cwd.XXXXXX")" cwd
    yazi "$@" --cwd-file="$tmp"
    if cwd="$(command cat -- "$tmp")" && [ -n "$cwd" ] && [ "$cwd" != "$PWD" ]; then
        builtin cd -- "$cwd"
    fi
    rm -f -- "$tmp"
}

# Adding starship prompt for easy and customized shell prompt
eval "$(starship init zsh)"

# Adding atuin for fuzzy searching command history
eval "$(atuin init zsh)"

# zoxide
eval "$(zoxide init zsh)"

